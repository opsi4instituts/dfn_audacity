[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub32.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub32.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) 32 Bit..."	

	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode

	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry32_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge
	set $ResultList$ = removeFromListByContaining("LAME", $ResultList$)

	Switch count ($ResultList$)
		Case "0"
			logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
			isFatalError
		EndCase
		Case "1"
			comment "Successful Installation"
			comment "Get 'DisplayIcon' from registry for path to exe"
			Set $RegId$ = takeSTring(0, $ResultList$)
			Set $PathToExe$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")

			if $DesktopLink$ = "true"
				comment "Create common desktop link"
				Linkfolder_create_desktoplink	
			endif

			comment "include custom post install file"
			set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
			if not ($CustomPostInstall$ = "none")
				if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
					include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
				endif
			endif
		EndCase
	EndSwitch
endif

[Winbatch_install]
"%ScriptPath%\$InstallProgram$" /sp- $SilentOption$ /norestart /LOADINF="%SCRIPTPATH%\audacity.ini" /nocancel /SUPPRESSMSGBOXES

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_link
	name: $ProductName$
	target: $PathToExe$
end_link


